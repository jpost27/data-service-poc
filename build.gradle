plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.25.0"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

subprojects {
    group = 'com.jp'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    java {
        sourceCompatibility = '21'
    }
    repositories {
        mavenCentral()
    }

    spotless {
        java {
            ratchetFrom 'origin/main'
            target '**/src/*/java/**/*.java','**/src/test/*/java/**/*.java'
            importOrder()
            removeUnusedImports()
            toggleOffOn()
            palantirJavaFormat()
            endWithNewline()
            custom 'noWildcardImports', {
                if (it.contains('*;\n')) {
                    throw new Error('No wildcard imports allowed')
                }
            }
        }
        format 'json', {
            target '**/*.json'
            endWithNewline()
        }
        format 'terraform', {
            target '**/*.tf'
            endWithNewline()
        }
    }

    compileJava.dependsOn 'spotlessApply'
}

tasks.named('test') {
    useJUnitPlatform()
}
